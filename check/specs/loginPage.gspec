@import common.gspec

@objects
    login_box           #login-page
    login_caption       #login-page h2
    username_textfield  input[name='login.username']
    password_textfield  input[name='login.password']
    login_button        .button-login
    cancel_button       .button-cancel

@groups
    (login_box_button, login_box_buttons)                   login_button, cancel_button
    (login_box_textfield, login_box_textfields)             username_textfield, password_textfield
    (login_box_form_element, login_box_form_elements)       login_caption, &login_box_textfields
    (login_box_element, login_box_elements)                 &login_box_form_elements, &login_box_buttons

@set
    login_box_elements_vertical_margin  5 to 20px
    login_box_elements_side_margin      ~ 40px
    login_box_desktop_width             400px
    login_caption_text                  Login
    login_button_text                   Login
    cancel_button_text                  Cancel

@on mobile
    @set login_box_elements_side_margin 0px


= Login box =
    login_box:
        | located at the top inside content with ${content_vertical_margin} margin
        centered horizontally inside content 1px

    | &login_box_elements should be more or less readable
    | every &login_box_textfield has height ${form_textfield_height}
    | &login_box_buttons should be tapable

    = Styling =
        | login_button should look like a primary button
        | cancel_button should look like a secondary button

    @on desktop, tablet
        | &login_box_form_elements are aligned vertically above each other with ${login_box_elements_vertical_margin} margin
        | first &login_box_form_element is inside login_box ${login_box_elements_side_margin} top
        | last &login_box_form_element is above login_button ${login_box_elements_vertical_margin}
        | &login_box_buttons are aligned horizontally next to each other with 0 to 5px margin
    @on mobile
        | &login_box_elements are aligned vertically above each other with ${login_box_elements_vertical_margin} margin
        | &login_box_elements sides are vertically inside login_box with ${login_box_elements_side_margin} margin

    login_caption:
        text is "${login_caption_text}"

    login_button:
        text is "${login_button_text}"

    cancel_button:
        text is "${cancel_button_text}"
