@import common.gspec

@objects
    caption             #my-notes-page  h2
    notes_container     .list-group 
    note-*              .list-group a
    addnote_button      button


@groups
    (note, notes)       note-*
    (my_notes_element, my_notes_elements)   caption, notes_container, addnote_button
    

@set
    note_amount                 > 1
    notes_margin                10 to 30px
    addnote_button_text         Add note
    addnote_button_margin       ~ 20px
    almost_zero                 -1 to 0px

@on mobile
    @set
        addnote_button_margin   ~ 30px
    

= My notes =
    | test all &notes with components/my-note.gspec
    | amount of visible &notes should be ${note_amount}

    | &notes are aligned vertically above each other with ${almost_zero} margin
    | &notes sides are vertically inside notes_container

    | first &note is below caption ${notes_margin}
    | last &note is above addnote_button ${addnote_button_margin}

    | caption should be located at the top inside content with ${content_vertical_margin} margin
    | caption should stretch to content with ${content_horizontal_margin} margin

    addnote_button:
        text is "${addnote_button_text}"
        | is tapable
        @on desktop, tablet
            aligned vertically left caption

    @on desktop, tablet
        | caption, notes_container are aligned vertically above each other with ${notes_margin} margin
    @on mobile
        | &my_notes_elements are aligned vertically above each other with ${notes_margin} margin
        | &my_notes_elements sides are vertically inside content with > 0px margin

