@import common.gspec

@objects
    caption                 #content h2
    title_textfield         input[name='note.title']
    description_textfield   textarea
    add_button              button.btn-primary
    cancel_button           button.btn-default


@groups
    (add_note_form_element, add_note_form_elements)     caption, title_textfield, description_textfield
    (add_note_form_button, add_note_form_buttons)       add_button, cancel_button
    (add_note_element, add_note_elements)               &add_note_form_elements, &add_note_form_buttons


@set
    add_note_elements_vertical_margin   5 to 20px
    description_height                  150 to 350px


= Add note page =
    | caption should be located at the top inside content with ${content_vertical_margin} margin

    | &add_note_elements should be more or less readable
    | &add_note_form_elements should stretch to content with ${content_horizontal_margin} margin
    | &add_note_form_button should be tapable

    @on desktop, tablet
        | &add_note_form_elements are aligned vertically above each other with ${add_note_elements_vertical_margin} margin
        | last &add_note_form_element is above add_button ${add_note_elements_vertical_margin}
        | &add_note_form_buttons are aligned horizontally next to each other with 0 to 5px margin
    @on mobile
        | &add_note_elements are aligned vertically above each other with ${add_note_elements_vertical_margin} margin

    title_textfield:
        height ${form_textfield_height}
    
    description_textfield:
        height ${description_height}
